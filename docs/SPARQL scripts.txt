#Goal: find diseases associated with E. coli in human gut

PREFIX Escherichia: <http://purl.obolibrary.org/obo/NCBITaxon_561>
PREFIX disease: <http://purl.obolibrary.org/obo/DOID_4>
PREFIX microb_increased_in_human_gut_with_disease: <http://purl.obolibrary.org/obo/OHMI_0000059>
SELECT distinct (STR(?microbe_label) AS ?microbe_label) (STR(?disease_label) AS ?disease_label) 
FROM <http://purl.obolibrary.org/obo/merged/OHMI>
WHERE
{
   ?microbe rdfs:label ?microbe_label .
   ?disease rdfs:label ?disease_label .
   ?microbe rdfs:subClassOf Escherichia: option (transitive) . 
   ?disease rdfs:subClassOf disease: option (transitive) .
   ?microbe rdfs:subClassOf ?microbe_restriction . 
   ?microbe_restriction owl:onProperty microb_increased_in_human_gut_with_disease:; owl:someValuesFrom ?disease .
}

--
#Goal: find host-microbiome interactions associated with E. coli in human gut

PREFIX Escherichia: <http://purl.obolibrary.org/obo/NCBITaxon_561>
PREFIX has_microb_susceptibly_expanded_in_diseaseed_host: <http://purl.obolibrary.org/obo/OHMI_0000423>
PREFIX human_gut_microbiome_interaction: <http://purl.obolibrary.org/obo/OHMI_0000021> 
SELECT distinct (STR(?microbe_label) AS ?microbe_label) (STR(?interaction_label) AS ?interaction_label) 
FROM <http://purl.obolibrary.org/obo/merged/OHMI>
WHERE
{
   ?microbe rdfs:label ?microbe_label .
   ?interaction rdfs:label ?interaction_label .
   ?microbe rdfs:subClassOf Escherichia: option (transitive) . 
   ?interaction rdfs:subClassOf human_gut_microbiome_interaction: option (transitive) .
   ?interaction rdfs:subClassOf ?interaction_restriction . 
   ?interaction_restriction owl:onProperty has_microb_susceptibly_expanded_in_diseaseed_host:; owl:someValuesFrom ?microbe .
}

--
#Goal: find all microbiome microbes associated with rheumatoid arthritis (RA) 
PREFIX organism: <http://purl.obolibrary.org/obo/PCO_0000031>
PREFIX RA: <http://purl.obolibrary.org/obo/DOID_7148>
#PREFIX microb_increased_in_human_gut_with_disease: <http://purl.obolibrary.org/obo/OHMI_0000272>
SELECT distinct ?microbe (STR(?microbe_label) AS ?microbe_label) ?relation_label (STR(?disease_label) AS ?disease_label) 
#SELECT count(distinct ?microbe) 
FROM <http://purl.obolibrary.org/obo/merged/OHMI>
WHERE
{
   ?microbe rdfs:label ?microbe_label .
   RA: rdfs:label ?disease_label .
   ?relation rdfs:label ?relation_label .
   ?microbe rdfs:subClassOf organism: option (transitive) . 
   ?microbe rdfs:subClassOf ?microbe_restriction . 
   ?microbe_restriction owl:onProperty ?relation; owl:someValuesFrom RA: .
}
